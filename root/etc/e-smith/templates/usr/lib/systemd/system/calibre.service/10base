{
    use esmith::DB::db;

    my $caldb   = esmith::DB::db->open_ro('calibre') || die("Can't open calibre db.");
    my $options = '';

    # get values from db
    my $auth            = $calibre{'Auth'} || 'disabled';
    my $auth_mode       = $calibre{'AuthMode'} || '';
    my $ban_after       = $calibre{'BanAfter'} || '';
    my $ban_for         = $calibre{'BanFor'} || '';
    my $num_per_page    = $calibre{'NumPerPage'} || '';
    my $opds_items      = $calibre{'MaxOPDSItems'} || '';
    my $opds_ung_items  = $calibre{'MaxOPDSUngroupedItems'} || '';
    my $port            = $calibre{'TCPPort'} || '8008';
    my $prefix          = $calibre{'URLPrefix'} || 'calibre';
    my $data_dir        = '/var/lib/nethserver/calibre';
    my $libraries_dir   = $data_dir . '/libraries';
    my $default_library = $libraries_dir . '/Books';
    my $userdb          = $calibre{'UserDB'} || "$data_dir/.config/calibre/server-users.sqlite";
    my $vhost           = $calibre{'VirtualHost'} || '';
    my $pattern         = qr([:"|><*?\\]|(\/|^)\.{1,2}(\/|$)); # exclusion pattern, matches ':"|><*?\', './', '../', '/..';
    my @libraries;


    # process libraries
    my @libs = $caldb->get_all_by_prop('type' => 'library');
    if ( ! @libs) {die("[ERROR] No libraries.");}
    foreach my $lib (@libs) {
        # get path and trim whitespace
        my $path = $lib->prop('Path');
        $path =~ s/^\s+|\s+$//;

        if ( ! $path) {
            warn("[NOTICE] '$lib->key' library path missing.\n");
            next;
        }

        # validate: use of restricted characters
        if ($path =~ m/$pattern/) {
            warn("[NOTICE] '$lib->key' library has an invalid path: '$path'\n");
            next;
        }

        # build absoulte path (consider File::Spec)
        $path =~ s{(?=^[^/])}{$libraries_dir/};
    
        # looks like a calibre library
        if (-e $path . '/metadata.db') {
            push @libraries, $path;
        }
    }
    if ( ! @libraries) {die("[ERROR] No libraries.");}


    ## Options: see https://manual.calibre-ebook.com/generated/en/calibre-server.html

    # $options .= " --pidfile=/var/run/calibre.pid";
    if ($num_per_page =~ m/^\d+$/) { $options .= " --num-per-page=$num_per_page"; }

    # Connection
    $options .= " --listen-on=localhost";
    $options .= " --port=$port";
    if ( ! $vhost && $prefix) {$options .= " --url-prefix=\"/$prefix\"";}
    # if ($timeout =~ m/^\d+$/) { $options .= " --timeout=$timeout"; }

    # User Authentication
    if ($auth eq 'enabled') {
        $options .= " --enable-auth";
        if ($auth_mode) { $options .= " --auth-mode=$auth_mode";}
        $options .= " --userdb=\"$userdb\"";
        if ($ban_after =~ m/^\d+$/) {$options .= " --ban-after=$ban_after";}
        if ($ban_for =~ m/^\d+$/)   {$options .= " --ban-for=$ban_for";}
    }
    else {
        $options .= " --enable-local-write";
    }

    # Logs
    $options .= " --log=/var/log/calibre/calibre-server.log";
    $options .= " --access-log=/var/log/calibre/calibre-server-access.log";

    # OPDS feeds
    if ($opds_items =~ m/^\d+$/)     {$options .= " --max-opds-items=$opds_items"; }
    if ($opds_ung_items =~ m/^\d+$/) {$options .= " --max-opds-ungrouped-items=$opds_ung_items"; }

    # Libraries of books
    foreach (@libraries) {
        #$options .= ' "' . $_ . '"';
        $options .= " '$_'";
    }

    #
    # Calibre Content Server Service
    #
    $OUT .= "[Unit]\n";
    $OUT .= "Description=calibre content server\n";
    $OUT .= "After=network.target\n\n";

    $OUT .= "[Service]\n";
    $OUT .= "Type=simple\n";
    $OUT .= "User=calibre\n";
    $OUT .= "Group=calibre\n";
    $OUT .= "ExecStart=/usr/bin/calibre-server $options\n\n";

    $OUT .= "[Install]\n";
    $OUT .= "Alias=calibre-server\n";
    $OUT .= "WantedBy=multi-user.target\n";
}